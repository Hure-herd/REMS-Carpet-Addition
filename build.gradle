plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1' apply false
	id 'fabric-loom' version '1.9-SNAPSHOT' apply false
	id 'com.replaymod.preprocess' version '7fa8d51'
}

preprocess {
	def mc119 = createNode('1.19.4', 1_19_04, 'yarn')
	def mc1201 = createNode('1.20.1', 1_20_01, 'yarn')
	def mc1206 = createNode('1.20.6', 1_20_06, 'yarn')
	def mc121 = createNode('1.21', 1_21_00, 'yarn')
	def mc1211 = createNode('1.21.1', 1_21_01, 'yarn')
	def mc1214 = createNode('1.21.4', 1_21_04, 'yarn')

	mc119.link(mc1201, file('versions/mapping-1.19-1.20.txt'))
	mc1201.link(mc1206, file('versions/mapping-1.20.1-1.20.6.txt'))
	mc1206.link(mc121, file('versions/mapping-1.20.6-1.21.txt'))
	mc121.link(mc1211, file('versions/mapping-1.21-1.21.1.txt'))
	mc1211.link(mc1214, file('versions/mapping-1.21.1-1.21.4.txt'))
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.name + ':build'
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		copy {
			subprojects {
				def libDir = project.projectDir.toPath().resolve('build/libs')
				from(libDir) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar'
				}
				into 'build/libs/'
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
