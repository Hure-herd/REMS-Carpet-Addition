plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1' apply false
	id 'fabric-loom' version '1.14-SNAPSHOT' apply false
	id 'com.replaymod.preprocess' version '7fa8d51'
}

preprocess {
	def mc119 = createNode('1.19.4', 1_19_04, 'yarn')
	def mc1201 = createNode('1.20.1', 1_20_01, 'yarn')
	def mc1204 = createNode('1.20.4', 1_20_04, 'yarn')
	def mc1206 = createNode('1.20.6', 1_20_06, 'yarn')
	def mc121 = createNode('1.21', 1_21_00, 'yarn')
	def mc1211 = createNode('1.21.1', 1_21_01, 'yarn')
	def mc1212 = createNode('1.21.2', 1_21_02, 'yarn')
	def mc1214 = createNode('1.21.4', 1_21_04, 'yarn')
	def mc1215 = createNode('1.21.5', 1_21_05, 'yarn')
	def mc1218 = createNode('1.21.8', 1_21_08, 'yarn')
	def mc12110 = createNode('1.21.10', 1_21_10, 'yarn')
	def mc12111 = createNode('1.21.11', 1_21_11, 'yarn')

	mc119.link(mc1201, file('versions/mapping-1.19-1.20.1.txt'))
	mc1201.link(mc1204, file('versions/mapping-1.20.1-1.20.4.txt'))
	mc1204.link(mc1206, file('versions/mapping-1.20.4-1.20.6.txt'))
	mc1206.link(mc121, file('versions/mapping-1.20.6-1.21.txt'))
	mc121.link(mc1211, file('versions/mapping-1.21-1.21.1.txt'))
	mc1211.link(mc1212, file('versions/mapping-1.21.1-1.21.2.txt'))
	mc1212.link(mc1214, file('versions/mapping-1.21.2-1.21.4.txt'))
	mc1214.link(mc1215, file('versions/mapping-1.21.4-1.21.5.txt'))
	mc1215.link(mc1218, file('versions/mapping-1.21.5-1.21.8.txt'))
	mc1218.link(mc12110, file('versions/mapping-1.21.8-1.21.10.txt'))
	mc12110.link(mc12111, file('versions/mapping-1.21.10-1.21.11.txt'))
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.name + ':build'
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		copy {
			subprojects {
				def libDir = project.projectDir.toPath().resolve('build/libs')
				from(libDir) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar'
				}
				into 'build/libs/'
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
